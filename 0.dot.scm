(define (dot sexp . opts)
  (let* ((nodes '())
         (edges '())
         (curr-id 0)
         (node-make (lambda (id)
                      (let ((node-id (format false "p~a" id))
                            (node-car false)
                            (node-cdr false))
                        (lambda (m)
                          (cond ((eq? m 'id) node-id)
                                ((eq? m 'car)
                                 (lambda (val) (set! node-car val)))
                                ((eq? m 'cdr)
                                 (lambda (val) (set! node-cdr val)))
                                ((eq? m 'fmt)
                                 (format false "~a[shape=record label=\"<car>~a|<cdr>~a\"]; "
                                         node-id node-car node-cdr))
                                (else
                                 (error "Unknown request -- MAKE-NODE" m)))))))
         (node-add (lambda ()
                     (set! curr-id (1+ curr-id))
                     (set! nodes (cons (node-make curr-id)
                                       nodes))))
         (node-curr (lambda () (car nodes)))
         (node-fmt (lambda (node) (node 'fmt)))
         (edge-add (lambda (head-node head-src tail-node tail-src)
                     (set! edges (cons (format false "~a:~a -> ~a:~a; "
                                               (head-node 'id) head-src
                                               (tail-node 'id) tail-src)
                                       edges)))))
    (let loop ((el sexp)
               (reg false)
               (src-node false))
      (if (pair? el)
          (begin (node-add)
                 (let ((new-node (node-curr)))
                   (when src-node
                     ((src-node reg) ".")
                     (edge-add src-node reg
                               new-node 'car))
                   (loop (car el) 'car new-node)
                   (loop (cdr el) 'cdr new-node)))
          ((src-node reg) (if (null? el) "/" el))))
    (apply string-append
           (reduce-left append '()
                        (list (list "digraph { ")
                              opts
                              (reverse (map node-fmt nodes))
                              (reverse edges)
                              (list "}"))))))
